/-
Extracted from CategoryTheory/SmallObject/Iteration/FunctorOfCocone.lean
Genuine: 12 of 12 | Dissolved: 0 | Infrastructure: 0
-/
import Origin.Core

/-!
# The functor from `Set.Iic j` deduced from a cocone

Given a functor `F : Set.Iio j ⥤ C` and `c : Cocone F`, we define
an extension of `F` as a functor `Set.Iic j ⥤ C` for which
the top element is mapped to `c.pt`.

-/

universe u

namespace CategoryTheory

open Category Limits

namespace SmallObject

namespace SuccStruct

variable {C : Type*} [Category* C]
  {J : Type u} [LinearOrder J]
  {j : J} {F : Set.Iio j ⥤ C} (c : Cocone F)

namespace ofCocone

def obj (i : J) : C :=
  if hi : i < j then
    F.obj ⟨i, hi⟩
  else c.pt

def objIso (i : J) (hi : i < j) :
    obj c i ≅ F.obj ⟨i, hi⟩ :=
  eqToIso (dif_pos hi)

def objIsoPt :
    obj c j ≅ c.pt :=
  eqToIso (dif_neg (by simp))

def map (i₁ i₂ : J) (hi : i₁ ≤ i₂) (hi₂ : i₂ ≤ j) :
    obj c i₁ ⟶ obj c i₂ :=
  if h₂ : i₂ < j then
    (objIso c i₁ (lt_of_le_of_lt hi h₂)).hom ≫ F.map (homOfLE hi) ≫ (objIso c i₂ h₂).inv
  else
    have h₂' : i₂ = j := le_antisymm hi₂ (by simpa using h₂)
    if h₁ : i₁ < j then
      (objIso c i₁ h₁).hom ≫ c.ι.app ⟨i₁, h₁⟩ ≫ (objIsoPt c).inv ≫ eqToHom (by subst h₂'; rfl)
    else
      have h₁' : i₁ = j := le_antisymm (hi.trans hi₂) (by simpa using h₁)
      eqToHom (by subst h₁' h₂'; rfl)

lemma map_id (i : J) (hi : i ≤ j) :
    map c i i (by rfl) hi = 𝟙 _ := by
  dsimp [map]
  grind

lemma map_comp (i₁ i₂ i₃ : J) (hi : i₁ ≤ i₂) (hi' : i₂ ≤ i₃) (hi₃ : i₃ ≤ j) :
    map c i₁ i₃ (hi.trans hi') hi₃ =
      map c i₁ i₂ hi (hi'.trans hi₃) ≫
        map c i₂ i₃ hi' hi₃ := by
  obtain hi₁₂ | rfl := hi.lt_or_eq
  · obtain hi₂₃ | rfl := hi'.lt_or_eq
    · dsimp [map]
      obtain hi₃' | rfl := hi₃.lt_or_eq
      · rw [dif_pos hi₃', dif_pos (hi₂₃.trans hi₃'), dif_pos hi₃', assoc, assoc,
          Iso.inv_hom_id_assoc, ← Functor.map_comp_assoc, homOfLE_comp]
      · rw [dif_neg (by simp), dif_pos (hi₁₂.trans hi₂₃), dif_pos hi₂₃, dif_neg (by simp),
          dif_pos hi₂₃, eqToHom_refl, comp_id, assoc, assoc, Iso.inv_hom_id_assoc,
          Cocone.w_assoc]
    · rw [map_id, comp_id]
  · rw [map_id, id_comp]

end ofCocone

def ofCocone : Set.Iic j ⥤ C where
  obj i := ofCocone.obj c i.1
  map {_ j} f := ofCocone.map c _ _ (leOfHom f) j.2
  map_id i := ofCocone.map_id _ _ i.2
  map_comp {_ _ i₃} _ _ := ofCocone.map_comp _ _ _ _ _ _ i₃.2

lemma ofCocone_obj_eq (i : J) (hi : i < j) :
    (ofCocone c).obj ⟨i, hi.le⟩ = F.obj ⟨i, hi⟩ :=
  dif_pos hi

def ofCoconeObjIso (i : J) (hi : i < j) :
    (ofCocone c).obj ⟨i, hi.le⟩ ≅ F.obj ⟨i, hi⟩ :=
  ofCocone.objIso c _ _

lemma ofCocone_obj_eq_pt :
    (ofCocone c).obj ⟨j, by simp⟩ = c.pt :=
  dif_neg (by simp)

def ofCoconeObjIsoPt :
    (ofCocone c).obj ⟨j, by simp⟩ ≅ c.pt :=
  ofCocone.objIsoPt c

lemma ofCocone_map_to_top (i : J) (hi : i < j) :
    (ofCocone c).map (homOfLE hi.le) =
      (ofCoconeObjIso c i hi).hom ≫ c.ι.app ⟨i, hi⟩ ≫ (ofCoconeObjIsoPt c).inv := by
  dsimp [ofCocone, ofCocone.map, ofCoconeObjIso, ofCoconeObjIsoPt]
  rw [dif_neg (by simp), dif_pos hi, comp_id]
