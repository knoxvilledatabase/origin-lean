/-
Extracted from CategoryTheory/Limits/Types/Coequalizers.lean
Genuine: 3 of 3 | Dissolved: 0 | Infrastructure: 0
-/
import Origin.Core

/-!
# Coequalizers in Type

The coequalizer of a pair of maps `(f, g)` from `X` to `Y`
is the quotient of `Y` by `∀ x : Y, f x ~ g x`

-/

universe v u

open CategoryTheory Limits ConcreteCategory

namespace CategoryTheory.Limits.Types

variable {X Y Z : Type u} (f g : X ⟶ Y)

def coequalizerColimit : Limits.ColimitCocone (parallelPair f g) where
  cocone :=
    Cofork.ofπ (TypeCat.ofHom (Function.Coequalizer.mk f g))
      (by ext x; exact Function.Coequalizer.condition f g x)
  isColimit :=
    Cofork.IsColimit.mk _
      (fun s ↦ TypeCat.ofHom (Function.Coequalizer.desc f g s.π
        (by ext x; exact ConcreteCategory.congr_hom s.condition x)))
      (fun _ ↦ rfl)
      (fun _ _ hm ↦ by ext x; exact Quot.inductionOn x (congr_hom hm))

theorem coequalizer_preimage_image_eq_of_preimage_eq (π : Y ⟶ Z) (e : f ≫ π = g ≫ π)
    (h : IsColimit (Cofork.ofπ π e)) (U : Set Y) (H : f ⁻¹' U = g ⁻¹' U) : π ⁻¹' (π '' U) = U := by
  have lem : ∀ x y, Function.Coequalizer.Rel f g x y → (x ∈ U ↔ y ∈ U) := by
    rintro _ _ ⟨x⟩
    change x ∈ f ⁻¹' U ↔ x ∈ g ⁻¹' U
    rw [H]
  have eqv : _root_.Equivalence fun x y => x ∈ U ↔ y ∈ U := by
    aesop (add safe constructors _root_.Equivalence)
  ext
  constructor
  · rw [←
      show _ = π from
        h.comp_coconePointUniqueUpToIso_inv (coequalizerColimit f g).2
          WalkingParallelPair.one]
    rintro ⟨y, hy, e'⟩
    dsimp at e'
    have e'' :=
      (mono_iff_injective
            (h.coconePointUniqueUpToIso (coequalizerColimit f g).isColimit).inv).mp
        inferInstance
    refine (eqv.eqvGen_iff.mp (Relation.EqvGen.mono lem (Quot.eqvGen_exact ?_))).mp hy
    apply e''
    convert e'
  · exact fun hx => ⟨_, hx, rfl⟩

noncomputable def coequalizerIso : coequalizer f g ≅ (Function.Coequalizer f g) :=
  colimit.isoColimitCocone (coequalizerColimit f g)
